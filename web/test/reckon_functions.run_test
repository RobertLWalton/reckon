// Reckon Language Function Test Data
//
// File:        reckon_functions.rec
// Author:      Bob Walton (walton@acm.org)
// Date:        Mon Jul  7 03:09:58 PM EDT 2025
//
// The authors have placed this data in the public
// domain; they make no warranty and accept no liability
// for this data.

function add (X) (Y):
    Z = X + Y
    return Z

Z = add (5) (7)
12
next Z = add 2 3
5
next Z = add 2 "HI"
!!!!!!!!!!!!!!!!!!!!!!!!! ERROR: invalid operand(s)
*** {12:3,5,23} ADD: Z = nan <= 2 + "HI"
nan

function (X) sub (Y): return X - Y;

W = (5) sub (7)
-2

function sum from (X) to (Y):
    S = 0
    while X <= Y:
        next S + = X
        next X + = 1
    return S

sum from (1) to (10)
55

function sum (L):
    S = 0
    E = L[]
    until E == *NONE*:
        next S + = E
        next E = L[]
    return S

sum ([1, 2, 3, 4, 5, 6, 7, 8, 9, 10])
55

function multiplication table:
    function part table ( Y, L ):
        function part row ( Y, X, L ):
            L[] = Y * X
            if X == 9: return L
            else: return part row ( Y, X + 1, L )
        L[] = part row ( Y, 1, [] )
        if Y == 9: return L
        else: return part table ( Y + 1, L )
    return part table ( 1, [] )

multiplication table
[[1, 2, 3, 4, 5, 6, 7, 8, 9], [2, 4, 6, 8, 10, 12, 14, 16, 18],
 [3, 6, 9, 12, 15, 18, 21, 24, 27], [4, 8, 12, 16, 20, 24, 28, 32, 36],
 [5, 10, 15, 20, 25, 30, 35, 40, 45],
 [6, 12, 18, 24, 30, 36, 42, 48, 54],
 [7, 14, 21, 28, 35, 42, 49, 56, 63],
 [8, 16, 24, 32, 40, 48, 56, 64, 72],
 [9, 18, 27, 36, 45, 54, 63, 72, 81]]
