// Reckon Language Assignments Test Data
//
// File:	reckon_assignments.rec
// Author:	Bob Walton (walton@acm.org)
// Date:	Sun Nov 24 06:45:40 AM EST 2024
//
// The authors have placed this data in the public
// domain; they make no warranty and accept no liability
// for this data.

// Single Variable Expression Assignments
//
X = 5
Z = 8
Y = Z

Y = 10
    // It is illegal to reassign Y.
next Y = 10
    // It is legal to assign a next Y.

next Z = - Z
next Z = X + Z
next Z = X + Y - Z
next Z = X + ( Y - Z )

// Multi-Variable Expression Assignments
//
M, N = 5, 10
    // The values of M and N are printed in order
    // after the statement

next M, next N = N, M


P, Q = M * N, M + N

R, next P = P / Q, P + 1

// Block Assignments
//
E, F =:
    E = 25
    F = -25

E, F // Outputs first the value of E and then the value of F.

next E, next F = do:
    // The `do' here is cosmetic and has no effect.
    next E = 35
        // This does not take effect until after
        // this subblock has executed
    next F = E
        // The value of E is the value before the
        // subblock executes
E, F

J, K = do:
    J = E
    K = J + 1
        // J is write-only inside this subblock

// If statements in blocks.
//
S = 25
next S =:
    if S > 20: next S = 20
        // S = 20 is a restricted statement.  It is an
        // operand of `if ... : ... ' and cannot have
        // operators with precedence 0000.  It cannot
        // allocate variables, but can write variables
        // that are write-only.
S

T = 25
SIGN T = do:
    if T < 0: SIGN T = -1
    else if T == 0: SIGN T = 0
    else: SIGN T = +1
SIGN T

next T = do:
    if T < -20:
        JX = 5        // Allocates JX to block.
        next T = -20   // Just writes next SIGN T.
        // JX is deallocated here (end of its block)
    else if T > 20:
        JX = 6
        next T = 20
T
JX    // JX has been deallocated.


// Next variable implication
//
D = 95
do:
    // In `next D = do' the next D is implied by the
    // following statement.
    next D = D + 10
D
do:
    // In `next D = do' the next D is implied by the
    // statement `next D = 33'.

    JX = 50
    do:
        // In `next JX = do' the next JX is implied by
        // the statement `next JX = 60'.

        next D = 33
        next JX = 60
    // JX is deallocated here
D

// Variable modifying statements
//
W = 13
next W =:
    next W + = 4
next W =:
    next W * = 10
do:
    next W / = 10
    next W - = 4
do:
    next W = 1
    do:
        next W << = 10 
do:
    next W >> = 10 


// Exit statements in blocks.
//
exit

X1 =:
    X1 = 5
    X1 = 15
    exit
    X1 = 25

X1

X2 = do BLOCK2:
    X2 = 5
    X2 = 15
    do BLOCK3:
        exit BLOCK2
    X2 = 25

X2

